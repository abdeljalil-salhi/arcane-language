statements  :   NEWLINE* expr ( NEWLINE+ expr )* NEWLINE*

expr        :   KEYWORD:auto IDENTIFIER EQ expr
            :   comp-expr ( ( KEYWORD:and | KEYWORD:or ) comp-expr )*

comp-expr   :   KEYWORD:not comp-expr
            :   arith-expr ( ( EEQ | NEQ | LT | LTE | GT | GTE ) arith-expr )*

arith-expr  :   term ( ( PLUS | MINUS ) term )*

term        :   factor ( ( MUL | DIV | MOD ) factor )*

factor      :   ( PLUS | MINUS ) factor
            :   power

power       :   call ( ( POW ) factor )*

call        :   atom ( LPAREN ( expr ( COMMA expr )* )? RPAREN )*

atom        :   INT | FLOAT | STRING | IDENTIFIER
            :   LPAREN expr RPAREN
            :   list-expr
            :   if-expr
            :   for-expr
            :   while-expr
            :   func-def

list-expr   :   LSQUARE ( expr ( COMMA expr )* )? RSQUARE

if-expr     :   KEYWORD:if expr KEYWORD:then
                ( expr elif-expr | else-expr? )
            |   ( NEWLINE statements KEYWORD:end | elif-expr | else-expr )

elif-expr   :   KEYWORD:elif expr KEYWORD:then
                ( expr elif-expr | else-expr? )
            |   ( NEWLINE statements KEYWORD:end | elif-expr | else-expr )

else-expr   :   KEYWORD:else
                expr
            |   ( NEWLINE statements KEYWORD:end )

for-expr    :   KEYWORD:for IDENTIFIER EQ expr KEYWORD:to expr
                ( ( KEYWORD:increment | KEYWORD:decrement ) expr )? KEYWORD:then
                expr
            |   ( NEWLINE statements KEYWORD:end )

while-expr  :   KEYWORD:while expr KEYWORD:then
                expr
            |   ( NEWLINE statements KEYWORD:end )

func-def    :   KEYWORD:const IDENTIFIER?
                LPAREN ( IDENTIFIER ( COMMA IDENTIFIER )* )? RPAREN
                ( ARROW expr )
            |   ( NEWLINE statements KEYWORD:end )
