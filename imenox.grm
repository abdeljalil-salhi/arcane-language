expr        :   KEYWORD:auto IDENTIFIER EQ expr
            :   comp-expr ( ( KEYWORD:and | KEYWORD:or ) comp-expr )*

comp-expr   :   KEYWORD:not comp-expr
            :   arith-expr ( ( EEQ | NEQ | LT | LTE | GT | GTE ) arith-expr )*

arith-expr  :   term ( ( PLUS | MINUS ) term )*

term        :   factor ( ( MUL | DIV | MOD ) factor )*

factor      :   ( PLUS | MINUS ) factor
            :   power

power       :   atom ( ( POWER ) factor )*

atom        :   INT | FLOAT | IDENTIFIER
            :   LPAREN expr RPAREN
            :   if-expr

if-expr     :   KEYWORD:if expr KEYWORD:then expr
                (KEYWORD:elif expr KEYWORD:then expr)*
                (KEYWORD:else expr)?
